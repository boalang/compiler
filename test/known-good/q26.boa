ThrowTotal: output sum of int;
ThrowMax: output maximum(1) of string weight int;
ThrowMin: output minimum(1) of string weight int;
ThrowMean: output mean of int;

p: Project = input;

throws_cur_val := 0;
throws_s: stack of int;

q26 := visitor {
	before node: CodeRepository -> {
		snapshot := getsnapshot(node, "SOURCE_JAVA_JLS");
		foreach (i: int; def(snapshot[i]))
			visit(snapshot[i]);
		stop;
	}
	before node: Method -> {
		push(throws_s, throws_cur_val);
		throws_cur_val = 0;
	}
	after node: Method -> {
		ThrowTotal << throws_cur_val;
		ThrowMax << p.id weight throws_cur_val;
		ThrowMin << p.id weight throws_cur_val;
		ThrowMean << throws_cur_val;
		throws_cur_val = pop(throws_s);
	}
	before node: Statement ->
		if (node.kind == StatementKind.THROW)
			throws_cur_val++;
};

visit(p, q26);
