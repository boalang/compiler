# -------------- classifier - multischeme -------------- 
# Application: estimate refactoring count in the commit history
# --------------------------------------------------- 
# Input X: no. of first half revisions, no. of refactoring revisions in the first half revisions
# Output y: If the no. of refactoring revisions in second half revisions is larger.
p: Project = input;
type T = enum {y = "y", n = "n"};
ms: output multischeme(
    "-s 75 "
	 + " -B weka.classifiers.rules.OneR"
	 + " -B weka.classifiers.rules.ZeroR"
	 + " -B weka.classifiers.rules.JRip"
	 + " -B weka.classifiers.bayes.NaiveBayes"
	 + " -B weka.classifiers.bayes.BayesNet"    
) of { int, int, T };

size := getrevisionscount(p.code_repositories[0]);
first_half := 0;
second_half := 0;

is_refactoring_revision := function(r: Revision) : bool {
	msg := lowercase(r.log);
	patterns := { "refactor", "move", "extract", "rename", "pull up", "push down" };
	foreach (i: int; patterns[i])
		if (strfind(patterns[i], msg) > -1)
			return true;
	return false;
};

if (size != 0) {
	visit(p, visitor {
		before cr : CodeRepository -> {

			for (i := 0; i < size; i++) {
				r := getrevision(cr, i);
				if (is_refactoring_revision(r)) {
					if(size / 2 < i)
						first_half = first_half + 1;
					else
						second_half = second_half + 1;
				}
			}
			
			x1 := size / 2;
			x2 := first_half;
			y: T = T.n;
			if (second_half >= first_half)
				y = T.y;

			ms << { x1, x2, y };
			stop;
		}
	});	
}